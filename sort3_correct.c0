#use <conio>
#use <string>

void swap(string[] S, int i, int j) {
  string tmp = S[i];
  S[i] = S[j];
  S[j] = tmp;
}

// Partition implemented correctly.
int partition(string[] S, int lower, int pivot_idx, int upper) {
  string pivot = S[pivot_idx];
  swap(S, pivot_idx, upper-1);

  int left = lower;
  int right = upper-2; // upper-2, not upper-1

  while (left <= right) {
    // case insensitive comparison.
    if (string_compare(string_tolower(S[left]), string_tolower(pivot))< 0) {
      left++;
    } else {
      swap(S, left, right);
      right--;
    }
  }

  swap(S, left, upper-1);
  // return the correct index (left)
  return left;
}

void sort(string[] S, int lower, int upper) {
  if (upper-lower <= 1) {
    return;
  } else {
    // using the middle element as the pivot
    int pivot_idx = lower + (upper-lower)/2; 
    int mid = partition(S, lower, pivot_idx, upper);
    sort(S, lower, mid);
    sort(S, mid + 1, upper);
  }
}

int main() {
  bundle_t S = read_lines("favorites.txt");
  int length = string_bundle_length(S);
  string[] favs = string_bundle_array(S);

  println("Before:");
  for (int i = 0; i < length; i++) {
    printf("(%d) %s\n", i, favs[i]);
  }
  println("--------------------");

  sort(favs, 0, length);

  println("After:");
  for (int i = 0; i < length; i++) {
    printf("(%d) %s\n", i, favs[i]);
  }
  println("--------------------");

  return 0;
}